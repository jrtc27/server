
MACRO(MY_SET_LINKER_FLAG flag_to_set)
  FOREACH(linktype SHARED MODULE EXE)
    IF(ARGN)
      FOREACH(type ${ARGN})
        SET(CMAKE_${linktype}_LINKER_FLAGS_${type}
          "${CMAKE_${linktype}_LINKER_FLAGS_${type}} ${flag_to_set}" PARENT_SCOPE)
      ENDFOREACH()
    ELSE()
      SET(CMAKE_${linktype}_LINKER_FLAGS
        "${CMAKE_${linktype}_LINKER_FLAGS} ${flag_to_set}" PARENT_SCOPE)
    ENDIF()
  ENDFOREACH()
ENDMACRO()

FUNCTION(MY_CHECK_AND_SET_LINKER_FLAG flag_to_set)
  # Let's avoid expensive compiler tests on Windows:
  IF(WIN32)
    RETURN()
  ENDIF()

  IF(CMAKE_VERSION VERSION_GREATER_EQUAL 3.18.0)
    CMAKE_POLICY(SET CMP0057 NEW)
    INCLUDE(CheckLinkerFlag)
    FOREACH(lang C CXX)
      STRING(REGEX REPLACE "[-,= +]" "_" result "HAVE_LINK_FLAG_${lang}_${flag_to_set}")
      CHECK_LINKER_FLAG(${lang} ${flag_to_set} ${result})
      IF (${result})
        MY_SET_LINKER_FLAG(${flag_to_set})
      ENDIF()
    ENDFOREACH()
  ELSEIF(CMAKE_VERSION VERSION_GREATER_EQUAL 3.14.0)
    INCLUDE(CheckCXXSourceCompiles)
    STRING(REGEX REPLACE "[-,= +]" "_" result "HAVE_LINK_FLAG_${flag_to_set}")
    SET(SAVE_CMAKE_REQUIRED_LINK_OPTIONS "${CMAKE_REQUIRED_LINK_OPTIONS}")
    STRING(REGEX REPLACE "^-Wno-" "-W" flag_to_check ${flag_to_set})
    SET(CMAKE_REQUIRED_LINK_OPTIONS ${CMAKE_REQUIRED_LINK_OPTIONS} ${flag_to_check})
    CHECK_CXX_SOURCE_COMPILES("int main(void) { return 0; }" ${result})
    SET(CMAKE_REQUIRED_LINK_OPTIONS "${SAVE_CMAKE_REQUIRED_LINK_OPTIONS}")
    IF (${result})
      MY_SET_LINKER_FLAG(${flag_to_set})
    ENDIF()
  ELSE()
    STRING(REGEX REPLACE "[-,= +]" "_" result "HAVE_CXX_FLAG_${flag_to_set}")
    INCLUDE(check_compiler_flag)
    MY_CHECK_CXX_COMPILER_FLAG(${flag_to_set})
    IF (${result})
      MY_SET_LINKER_FLAG(${flag_to_set})
    ENDIF()
  ENDIF()
ENDFUNCTION()
